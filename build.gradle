group 'jnonino.devops.jenkins'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

apply plugin: 'groovy'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    jcenter()
}

configurations {
    testPlugins {}
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.+'
    compile "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"
    compile "org.jenkins-ci.main:jenkins-core:${jenkinsVersion}"

    // JUnit
    testCompile group: 'junit', name: 'junit', version: '+'

    // Spock dependencies
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'cglib:cglib-nodep:+'

    // Jenkins test harness dependencies
    testCompile ('org.jenkins-ci.main:jenkins-test-harness:+') {
        exclude(module: 'org-netbeans-insane')
    }
    testCompile "org.jenkins-ci.main:jenkins-core:${jenkinsVersion}"
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.10@jar'

    // Plugins to install in test instance
    // Job Configurations
    testPlugins group: 'org.jenkins-ci.plugins', name: 'job-dsl', version: "${jobDslVersion}"
    //testPlugins group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-aggregator', version: '2.5'
    testPlugins group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-multibranch', version: '+'
    // SCM
    testPlugins group: 'org.jenkins-ci.plugins', name: 'git', version: '+'
    // Build Tools
    testPlugins group: 'org.jenkins-ci.main', name: 'maven-plugin', version: '+'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'gradle', version: '+'
    // Static Analysis
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'checkstyle', version: '+'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'pmd', version: '+'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'findbugs', version: '+'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'tasks', version: '+'
    // Test
    testPlugins group: 'org.jenkins-ci.plugins', name: 'jacoco', version: '+'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'cobertura', version: '+'
    // Views
    testPlugins group: 'org.jenkins-ci.plugins', name: 'nested-view', version: '+'
    // Jenkins Settings
    testPlugins group: 'org.jenkins-ci.plugins', name: 'greenballs', version: '+'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'ci-game', version: '+'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'emotional-jenkins-plugin', version: '+'
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]
    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }
    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    ignoreFailures true
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate JaCoCo coverage reports after running tests."
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
