group 'jnonino.devops.jenkins'
version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

apply plugin: 'groovy'
apply plugin: 'jacoco'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

configurations {
    testPlugins {}
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.11'
    compile 'org.jenkins-ci.plugins:job-dsl-core:1.66'
    compile 'org.jenkins-ci.main:jenkins-core:2.7.4'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // Spock dependencies
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.2'

    // Jenkins test harness dependencies
    testCompile ('org.jenkins-ci.main:jenkins-test-harness:2.8') {
        exclude(module: 'org-netbeans-insane')
    }
    testCompile 'org.jenkins-ci.main:jenkins-core:2.7.4'
    testCompile 'org.jenkins-ci.main:jenkins-war:2.7.4'
    testCompile 'org.jenkins-ci.main:jenkins-war:2.7.4:war-for-test@jar'

    // Job DSL plugin including plugin dependencies
    testCompile 'org.jenkins-ci.plugins:job-dsl:1.66'
    testCompile 'org.jenkins-ci.plugins:job-dsl:1.66@jar'
    testCompile 'org.jenkins-ci.plugins:structs:1.1@jar'

    // Plugins to install in test instance
    // Job Configurations
    testPlugins group: 'org.jenkins-ci.plugins', name: 'job-dsl', version: '1.66'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'jobConfigHistory', version: '2.16'
    testPlugins group: 'org.jenkins-ci.plugins.workflow', name: 'workflow-multibranch', version: '2.14'
    // Build Tools
    testPlugins group: 'org.jenkins-ci.main', name: 'maven-plugin', version: '2.15.1'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'gradle', version: '1.26'
    // SCM
    testPlugins group: 'org.jenkins-ci.plugins', name: 'git', version: '3.3.0'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'clone-workspace-scm', version: '0.6'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'multiple-scms', version: '0.6'
    // Static Analysis
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'checkstyle', version: '3.48'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'pmd', version: '3.47'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'findbugs', version: '4.70'
    testPlugins group: 'org.jvnet.hudson.plugins', name: 'tasks', version: '4.51'
    // Test
    testPlugins group: 'org.jenkins-ci.plugins', name: 'jacoco', version: '2.2.1'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'cobertura', version: '1.10'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'test-results-analyzer', version: '0.3.4'
    testPlugins group: 'de.esailors.jenkins', name: 'test-stability', version: '2.2'
    // Views
    testPlugins group: 'org.jenkins-ci.plugins', name: 'nested-view', version: '1.14'
    // Build Settings
    testPlugins group: 'org.jenkins-ci.plugins', name: 'build-timeout', version: '1.18'
    // Jenkins Settings
    testPlugins group: 'org.jenkins-ci.plugins', name: 'greenballs', version: '1.15'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'ci-game', version: '1.26'
    testPlugins group: 'org.jenkins-ci.plugins', name: 'emotional-jenkins-plugin', version: '1.2'
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    ignoreFailures true
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate JaCoCo coverage reports after running tests."
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}